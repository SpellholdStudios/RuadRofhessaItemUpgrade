//////////////////////////////////////////////////
// Tactics Mod Imp. Mages Script
//////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////
// PC not here? 
///////////////////////////////////////////////////////////////////////
IF
	!InMyArea(Player1)
 	CombatCounter(0)
THEN
	RESPONSE #100
    // Do nothing
END

///////////////////////////////////////////////////////////////////////
// Threat Detection
//
// Attacked? Spell cast on me? Someone shouted a warning?
///////////////////////////////////////////////////////////////////////
IF
	!Allegiance(Myself,ENEMY)
	OR(12)
		AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
		AttackedBy([PC],DEFAULT)
		AttackedBy([FAMILIAR],DEFAULT)
		AttackedBy([ALLY],DEFAULT)
		AttackedBy([CONTROLLED],DEFAULT)
		AttackedBy([CHARMED],DEFAULT)
		SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
		SpellCastOnMe([PC],0)
		SpellCastOnMe([FAMILIAR],0)
		SpellCastOnMe([ALLY],0)
		SpellCastOnMe([CONTROLLED],0)
		SpellCastOnMe([CHARMED],0)
THEN
	RESPONSE #100
		Enemy()
		Continue()
END

/////////////////////////////////////////////////////////////////////
// Paranoia!
//
// Liches and other mages are smart enough to notice that they are
// mysteriously dying even if the PCs are protected from Undead or
// otherwise invisible.
/////////////////////////////////////////////////////////////////////
IF
	Global("Paranoid","LOCALS",0)
	!Detect(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
	OR(5)
	// CheckStatLT(Myself,3,STONESKINS)
		HPPercentLT(Myself,100)
     //  Exists(LastHitter)
		Exists(LastAttackerOf(Myself))
     //  Exists(LastTargetedBy([GOODCUTOFF]))
		SpellCast([GOODCUTOFF],0)	// triggers on any spell
		SpellCastPriest([GOODCUTOFF],0)	// triggers on any spell
		SpellCastInnate([GOODCUTOFF],0)	// triggers on any ability
THEN
	RESPONSE #100
		SetGlobal("Paranoid","LOCALS",1)
		DisplayString(Myself,26703)		// ~Damn, where are they?~
		Continue()
END

IF
	Global("Paranoid","LOCALS",1)
	!Detect(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("UndeadHate","LOCALS")
	General(Myself,UNDEAD)	// GENERAL Undead
	!AreaCheck("AR1401")	// not in the temple ruins
	OR(6)
		InMyArea(Player1)
		InMyArea(Player2)
		InMyArea(Player3)
		InMyArea(Player4)
		InMyArea(Player5)
		InMyArea(Player6)
THEN
	RESPONSE #100
		SetGlobalTimer("UndeadHate","LOCALS",24)
		DisplayString(Myself,62975)		// ~I've heard enough! Show yourself, whatever you are, so I might destroy you!~
		DisplayString(Myself,57171)		// ~I'll kill you for trying to manipulate me!~
		ForceSpellRES("wwundead",Myself)
END

/////////////////////////////////////////////////////////////////////
// CPU Cycle Saver
/////////////////////////////////////////////////////////////////////
IF
	ActionListEmpty()
 	CombatCounter(0)
	!Detect(NearestEnemyOf(Myself))
	!Global("Paranoid","LOCALS",1)
	!Global("Prep","LOCALS",0)
	!GlobalTimerExpired("PrepTimer","LOCALS")
THEN
	RESPONSE #100
		NoAction()
END


/////////////////////////////////////////////////////////////////////
// PREP==0 - Created Defenses - Levels 18+
// Were we "created" near an enemy? If so, we probably have weak defenses
// up already. 
/////////////////////////////////////////////////////////////////////
IF
	Global("Prep","LOCALS",0)
	CheckStatGT(Myself,17,LEVEL)
	OR(2)
		See(NearestEnemyOf(Myself))
		!CombatCounter(0)
THEN
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_BLUR)
		ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
		ReallyForceSpell(Myself,WIZARD_SHADOW_DOOR)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		ReallyForceSpell(Myself,WIZARD_SPELL_TRAP)
		Continue()
END

/////////////////////////////////////////////////////////////////////
// PREP==0 - Created Defenses - Levels 12--18
/////////////////////////////////////////////////////////////////////
IF
	Global("Prep","LOCALS",0)
	CheckStatGT(Myself,12,LEVEL)
	CheckStatLT(Myself,18,LEVEL)
	OR(2)
		See(NearestEnemyOf(Myself))
		!CombatCounter(0)
THEN
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_BLUR)
		ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		Continue()
END

/////////////////////////////////////////////////////////////////////
// Prep==0 && Guard==0 - Guardian Creature - Level 13+
// Tough mages don't walk around alone!
/////////////////////////////////////////////////////////////////////
IF
	CheckStatGT(Myself,12,LEVEL)
	Global("Prep","LOCALS",0)
	Global("Guard","LOCALS",0)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		CreateCreatureObject("stalke",Myself,0,0,0)
		IncrementGlobal("Guard","LOCALS",1)
		Continue()
	RESPONSE #100
		CreateCreatureObject("elair01",Myself,0,0,0)
		IncrementGlobal("Guard","LOCALS",1)
		Continue()
	RESPONSE #100
		CreateCreatureObject("elear01",Myself,0,0,0)
		IncrementGlobal("Guard","LOCALS",1)
		Continue()
	RESPONSE #100
		CreateCreatureObject("elfir01",Myself,0,0,0)
		IncrementGlobal("Guard","LOCALS",1)
		Continue()
	RESPONSE #100
		CreateCreatureObject("skelwa",Myself,0,0,0)
		IncrementGlobal("Guard","LOCALS",1)
		Continue()
END


/////////////////////////////////////////////////////////////////////
// PREP - Initialize Prep Timer
/////////////////////////////////////////////////////////////////////
IF
	Global("Prep","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobalTimer("PrepTimer","LOCALS",0)
		SetGlobal("Prep","LOCALS",1)
END

/////////////////////////////////////////////////////////////////////
// PrepTimer==0 - Short Spell Protections - Level 12-
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("PrepTimer","LOCALS")
	!CheckStatGT(Myself,12,LEVEL)
	OR(2)
		See(NearestEnemyOf(Myself))
		!CombatCounter(0)
THEN
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_BLUR)
		ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_WEAPONS)
		Continue()
END

/////////////////////////////////////////////////////////////////////
// PrepTimer==0 - Short Spell Protections - Level 13+
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("PrepTimer","LOCALS")
	CheckStatGT(Myself,12,LEVEL)
	OR(2)
	 	See(NearestEnemyOf(Myself))
	 	!CombatCounter(0)
THEN
	RESPONSE #100
		DisplayString(Myself,31247) // Tattoo of Power - Spell Immunity
		ApplySpell(Myself,WIZARD_SPELL_IMMUNITY_INVOCATION)
		Continue()
	RESPONSE #100
		DisplayString(Myself,31247) // Tattoo of Power - Spell Immunity
		ApplySpell(Myself,WIZARD_SPELL_IMMUNITY_ENCHANTMENT)
		Continue()
	RESPONSE #500
		DisplayString(Myself,31247) // Tattoo of Power - Spell Immunity
		ApplySpell(Myself,WIZARD_SPELL_IMMUNITY_ABJURATION)
		// blocks dispel magic
		Continue()
END

/////////////////////////////////////////////////////////////////////
// Potion==0 - Drink a Potion
// Every good mage has a potion or two in reserve ...
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("PrepTimer","LOCALS")
	Global("Potion","LOCALS",0)
	Allegiance(Myself,ENEMY)
	!HP(Myself,0)
THEN
  	RESPONSE #100
		GiveItemCreate("potn42",Myself,1,0,0)   // regeneration
		UseItem("potn42",Myself)
		SetGlobal("Potion","LOCALS",1)
		Continue()
  	RESPONSE #100
		GiveItemCreate("potn11",Myself,1,0,0)   // invuln
		UseItem("potn11",Myself)
		SetGlobal("Potion","LOCALS",1)
		Continue()
  	RESPONSE #100
		GiveItemCreate("potn34",Myself,1,0,0)   // 50% MR
		UseItem("potn34",Myself)
		SetGlobal("Potion","LOCALS",1)
		Continue()
  	RESPONSE #100
		GiveItemCreate("potn35",Myself,1,0,0)   // 50% MDR, +20 saves
		UseItem("potn35",Myself)
		SetGlobal("Potion","LOCALS",1)
		Continue()
END

/////////////////////////////////////////////////////////////////////
// PrepTimer==0 - Long Protections - Level 13+
// Resets PrepTimer
//
// There is no reason for powerful wizards to be walking around without
// these long-lasting spell protections in place. 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("PrepTimer","LOCALS")
	!HP(Myself,0)
THEN
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_FIRE)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_COLD)
		Continue()
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_FIRE)
		Continue()
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_COLD)
		Continue()
	RESPONSE #100
		Continue()
END

IF
	GlobalTimerExpired("PrepTimer","LOCALS")
	!HP(Myself,0)
	CheckStatGT(Myself,12,LEVEL)
THEN
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_ACID)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_ELECTRICITY)
		Continue()
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_ELECTRICITY)
		Continue()
	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_ACID)
		Continue()
	RESPONSE #100
		Continue()
END

IF
	CheckStatGT(Myself,12,LEVEL)
	GlobalTimerExpired("PrepTimer","LOCALS")
	!HP(Myself,0)
THEN
  	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_MELF_METEOR)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
		ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
		ReallyForceSpell(Myself,WIZARD_ARMOR)
		SetGlobalTimer("PrepTimer","LOCALS",4800)
		SetGlobalTimer("Cast","LOCALS",0)
		SetGlobalTimer("TimeStop","LOCALS",0)
		SetGlobalTimer("SpellTrap","LOCALS",0)
		SetGlobalTimer("ProMagic","LOCALS",0)
		SetGlobalTimer("Shadow","LOCALS",0)
		SetGlobalTimer("Fire","LOCALS",0)
		Continue()
END

/////////////////////////////////////////////////////////////////////
// PrepTimer==0 - Long Protections - Level 12-
// Resets PrepTimer
/////////////////////////////////////////////////////////////////////
IF
	!CheckStatGT(Myself,12,LEVEL)
	GlobalTimerExpired("PrepTimer","LOCALS")
	!HP(Myself,0)
THEN
  	RESPONSE #100
		ReallyForceSpell(Myself,WIZARD_MELF_METEOR)
		ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
		ReallyForceSpell(Myself,WIZARD_ARMOR)
		SetGlobalTimer("PrepTimer","LOCALS",4800)
		SetGlobalTimer("Cast","LOCALS",0)
		SetGlobalTimer("TimeStop","LOCALS",0)
		SetGlobalTimer("SpellTrap","LOCALS",0)
		SetGlobalTimer("ProMagic","LOCALS",0)
		SetGlobalTimer("Shadow","LOCALS",0)
		SetGlobalTimer("Fire","LOCALS",0)
		Continue()
END

/////////////////////////////////////////////////////////////////////
// Chain==0 - Chain Contingency - Level 18+
// Also handles contingency. 
/////////////////////////////////////////////////////////////////////
IF
	CheckStatGT(Myself,17,LEVEL)
	Global("Chain","LOCALS",0)
	OR(8)
		See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
		SpellCastOnMe([GOODCUTOFF],0)
		SpellCastOnMe([PC],0)
		SpellCastOnMe([FAMILIAR],0)
		SpellCastOnMe([ALLY],0)
		SpellCastOnMe([CONTROLLED],0)
		SpellCastOnMe([CHARMED],0)
	!HP(Myself,0)
THEN
  	RESPONSE #100
		SetGlobal("Chain","LOCALS",1)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_FIRE)
		DisplayString(Myself,26328) // Chain Contingency
		ReallyForceSpell(Myself,WIZARD_SPELL_TRAP)
		ReallyForceSpell(Myself,WIZARD_MORDENKAINENS_SWORD)
		ReallyForceSpell(Myself,WIZARD_MISLEAD)
		SetGlobalTimer("SpellTrap","LOCALS",60)
		DisplayString(Myself,40252) // Contingency - Contingency—Protection From Magical Weapons
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SetGlobalTimer("ProMagic","LOCALS",24)
  	RESPONSE #100
		SetGlobal("Chain","LOCALS",1)
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_FIRE)
		DisplayString(Myself,26328) // Chain Contingency
		ReallyForceSpell(Myself,WIZARD_SPELL_TRAP)
		ReallyForceSpell(Myself,WIZARD_IMPROVED_INVISIBILITY)
		ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_DIVINATION)
		SetGlobalTimer("SpellTrap","LOCALS",60)
		DisplayString(Myself,40252) // Contingency - Contingency—Protection From Magical Weapons
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SetGlobalTimer("ProMagic","LOCALS",24)
  	RESPONSE #100
		SetGlobal("Chain","LOCALS",1)
		DisplayString(Myself,26328) // Chain Contingency
		ReallyForceSpell(Myself,WIZARD_SPELL_TRAP)
		ReallyForceSpell(Myself,WIZARD_ABI_DALZIMS_HORRID_WILTING)
		ReallyForceSpell(Myself,WIZARD_ABI_DALZIMS_HORRID_WILTING)
		SetGlobalTimer("SpellTrap","LOCALS",60)
		DisplayString(Myself,40252) // Contingency - Contingency—Protection From Magical Weapons
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SetGlobalTimer("ProMagic","LOCALS",24)
  	RESPONSE #100
		SetGlobal("Chain","LOCALS",1)
		DisplayString(Myself,26328) // Chain Contingency
		ReallyForceSpell(Myself,WIZARD_SPELL_TRAP)
		ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_ABJURATION)
		ReallyForceSpell(Myself,WIZARD_ABI_DALZIMS_HORRID_WILTING)
		SetGlobalTimer("SpellTrap","LOCALS",60)
		DisplayString(Myself,40252) // Contingency - Contingency—Protection From Magical Weapons
		ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SetGlobalTimer("ProMagic","LOCALS",24)
END

/////////////////////////////////////////////////////////////////////
// Contingency==0 - Contingency - Levels 16-
/////////////////////////////////////////////////////////////////////
IF
	!CheckStatGT(Myself,17,LEVEL)
    HPPercentLT(Myself,100)
	Global("Contingency","LOCALS",0)
	!HP(Myself,0)
THEN
  	RESPONSE #100
		SetGlobal("Contingency","LOCALS",1)
		DisplayString(Myself,29736) // Contingency - Contingency—Stoneskin
		ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
END

/////////////////////////////////////////////////////////////////////
// Our Spell choices in a vague order of preference. We use random 
// response numbers to vary things from fight to fight. 
//
// (1) No-Brainer Self Spells
//		2 Vocalize
//		6 Pro-Magic Weapons
//		4 Stoneskin
//		9 Time Stop
//		9 Spell Trap
//		7 Spell Turning
//		7 Spell Deflection
//		5 Minor Spell Turning
//		6 True Sight
//		X Sequencer		(fireshields)
//
/////////////////////////////////////////////////////////////////////
//
// (2) Find a target with Spell Protections and/or Immunity Effects
//		9 Spellstrike
//		8 Pierce Shield
//		7 Warding Whip
//		7 Ruby Ray
//		4 Secret Word
//		3 Spell Thrust
//		5 Breach
//
/////////////////////////////////////////////////////////////////////
//
// (3) Find a nearby target with immunity, haste, invis, bless, chant, duhm,
//     luck, aid, blur, mirror image, etc. 
//		3 Remove Magic
//
/////////////////////////////////////////////////////////////////////
//
// (4) Find a Summoned Creature
//		6 Death Spell   (if summoned nearby)
//
/////////////////////////////////////////////////////////////////////
//
// (5) Find a target w/ no Spell Protections, low magic res
//		X Spell Trigger
//		9 Imprisonment
//		4 Malison
//		8 Wilting
//		9 Banshee
//		8 Symbol Stun
//		7 power word stun
//		8 Symbol Fear
//		8 Symbol Death
//		9 power word kill
//		7 Mordy
//		5 Chaos (spell -4)
//		7 finger of death (spell -2)
//		5 domination (spell -2)
//		4 polymorph other (pet-poly -2)
//		4 confusion
//		6 flesh to stone (pet-poly)
//		6 disintegrate (spell)
//		6 chain lightning
//		5 cone of cold
//		3 hold person (spell)
//		3 flame arrow   (15d6)
//		3 skull trap    (15d6)
//		5 sunfire		 (15d6)
//		3 lightning bolt (10d6)
//		- mislead
//		- shadow door
//		X healing potions
//		5 animate dead
//		- invis stalker
//		- wyvern call
//		- ms 3
//		4 fire shield
//		3 slow  (Spell -4)
//		2 web
//		2 stinking cloud
//		5 cloudkill
//		2 acid arrow
//		2 schorcher
//		1 orb
//		? ms 2
//		? ms 1
//		1 magic missile
//		1 minor drain
/////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// (1) (1) (1) (1)
/////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// Vocalize
/////////////////////////////////////////////////////////////////////
IF
    StateCheck(Myself,STATE_SILENCED)
    HaveSpell(WIZARD_VOCALIZE)
	GlobalTimerExpired("Cast","LOCALS")
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(Myself,WIZARD_VOCALIZE)
END

/////////////////////////////////////////////////////////////////////
// Pro-Magery
/////////////////////////////////////////////////////////////////////
IF
    HaveSpell(WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
	OR(2)
	 	See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("ProMagic","LOCALS")
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SetGlobalTimer("ProMagic","LOCALS",24)
END

/////////////////////////////////////////////////////////////////////
// Pro-Magery (sub for nishruu)
/////////////////////////////////////////////////////////////////////
IF
    HaveSpell(WIZARD_SUMMON_NISHRUU)
	OR(2)
	 	See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("ProMagic","LOCALS")
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		RemoveSpell(WIZARD_SUMMON_NISHRUU)
		SpellNoDec(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SetGlobalTimer("ProMagic","LOCALS",24)
END

/////////////////////////////////////////////////////////////////////
// Pro-Magery (sub for true sight)
/////////////////////////////////////////////////////////////////////
IF
    HaveSpell(WIZARD_TRUE_SIGHT)
	OR(2)
	 	See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("ProMagic","LOCALS")
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		RemoveSpell(WIZARD_TRUE_SIGHT)
		SpellNoDec(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SetGlobalTimer("ProMagic","LOCALS",24)
END

/////////////////////////////////////////////////////////////////////
// Pro-Magery (sub for disintegrate)
/////////////////////////////////////////////////////////////////////
IF
    HaveSpell(WIZARD_DISINTEGRATE)
	OR(2)
	 	See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("ProMagic","LOCALS")
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		RemoveSpell(WIZARD_DISINTEGRATE)
		SpellNoDec(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		SetGlobalTimer("ProMagic","LOCALS",24)
END

/////////////////////////////////////////////////////////////////////
// Spell: Stoneskin (if out)
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_STONE_SKIN)
	CheckStatLT(Myself,1,STONESKINS)
		GlobalTimerExpired("Cast","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(Myself,WIZARD_STONE_SKIN)
END

/////////////////////////////////////////////////////////////////////
// Time Stop
/////////////////////////////////////////////////////////////////////
IF
    HaveSpell(WIZARD_TIME_STOP)
	OR(2)
	 	See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("TimeStop","LOCALS")
THEN
  	RESPONSE #100
		SetGlobalTimer("TimeStop","LOCALS",24)
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(Myself,WIZARD_TIME_STOP)
END

/////////////////////////////////////////////////////////////////////
// Time Stop (replaces Meteor Swarm)
/////////////////////////////////////////////////////////////////////
IF
    HaveSpell(WIZARD_METEOR_SWARM)
	OR(2)
	 	See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("TimeStop","LOCALS")
THEN
  	RESPONSE #100
		SetGlobalTimer("TimeStop","LOCALS",24)
		SetGlobalTimer("Cast","LOCALS",6)
		RemoveSpell(WIZARD_METEOR_SWARM)
		SpellNoDec(Myself,WIZARD_TIME_STOP)
END

/////////////////////////////////////////////////////////////////////
// Time Stop (replaces Gate)
/////////////////////////////////////////////////////////////////////
IF
    HaveSpell(WIZARD_GATE)
	OR(2)
	 	See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("TimeStop","LOCALS")
THEN
  	RESPONSE #100
		SetGlobalTimer("TimeStop","LOCALS",24)
		SetGlobalTimer("Cast","LOCALS",6)
		RemoveSpell(WIZARD_GATE)
		SpellNoDec(Myself,WIZARD_TIME_STOP)
END

/////////////////////////////////////////////////////////////////////
// Spell Trap
/////////////////////////////////////////////////////////////////////
IF
    HaveSpell(WIZARD_SPELL_TRAP)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("SpellTrap","LOCALS")
	OR(2)
	 	See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("SpellTrap","LOCALS",60)
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(Myself,WIZARD_SPELL_TRAP)
END

/////////////////////////////////////////////////////////////////////
// Spell Turning
/////////////////////////////////////////////////////////////////////
IF
    HaveSpell(WIZARD_SPELL_TURNING)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("SpellTrap","LOCALS")
	OR(2)
	 	See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("SpellTrap","LOCALS",60)
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(Myself,WIZARD_SPELL_TURNING)
END

/////////////////////////////////////////////////////////////////////
// Spell Deflection
/////////////////////////////////////////////////////////////////////
IF
    HaveSpell(WIZARD_SPELL_DEFLECTION)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("SpellTrap","LOCALS")
	OR(2)
	 	See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("SpellTrap","LOCALS",60)
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(Myself,WIZARD_SPELL_DEFLECTION)
END

/////////////////////////////////////////////////////////////////////
// Minor Spell Turning
/////////////////////////////////////////////////////////////////////
IF
    HaveSpell(WIZARD_MINOR_SPELL_TURNING)
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("SpellTrap","LOCALS")
	OR(2)
	 	See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("SpellTrap","LOCALS",60)
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(Myself,WIZARD_MINOR_SPELL_TURNING)
END

/////////////////////////////////////////////////////////////////////
// True Sight
/////////////////////////////////////////////////////////////////////
IF
	Allegiance(Myself,ENEMY)
	OR(2)
		Detect([PC])
		Detect(NearestEnemyOf(Myself))
	!See(NearestEnemyOf(Myself))
	GlobalTimerExpired("Cast","LOCALS")
THEN
	RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    ForceSpell(Myself,WIZARD_TRUE_SIGHT)
END

/////////////////////////////////////////////////////////////////////
// Spell: Sequencer (defensive)
/////////////////////////////////////////////////////////////////////
IF
	CheckStatGT(Myself,13,LEVEL)
	GlobalTimerExpired("Cast","LOCALS")
	Global("Sequencer","LOCALS",0)
	OR(2)
		See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		SetGlobal("Sequencer","LOCALS",1)
		DisplayString(Myself,25951) // Sequencer - Spell Sequencer
		ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_RED)
		ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_BLUE)
		ReallyForceSpell(Myself,WIZARD_IMPROVED_INVISIBILITY)
END

/////////////////////////////////////////////////////////////////////
// (2) (2) (2) (2)
// Find a target with Spell Protections
/////////////////////////////////////////////////////////////////////

// Sixth Nearest.
IF
	!CheckStat(SixthNearestEnemyOf(Myself),0,STONESKINS)
	!StateCheck(SixthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
	See(SixthNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
// Fifth Nearest.
IF
	!CheckStat(FifthNearestEnemyOf(Myself),0,STONESKINS)
	!StateCheck(FifthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
	See(FifthNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
// Fourth Nearest.
IF
	!CheckStat(FourthNearestEnemyOf(Myself),0,STONESKINS)
	!StateCheck(FourthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
	See(FourthNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
// Third Nearest.
IF
	!CheckStat(ThirdNearestEnemyOf(Myself),0,STONESKINS)
	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
	See(ThirdNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
// Second Nearest.
IF
	!CheckStat(SecondNearestEnemyOf(Myself),0,STONESKINS)
	!StateCheck(SecondNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
	See(SecondNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
//  Nearest.
IF
	!CheckStat(NearestEnemyOf(Myself),0,STONESKINS)
	!StateCheck(NearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
	See(NearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
// Nearest Bard.
IF
	!CheckStat(NearestEnemyOfType([GOODCUTOFF.0.0.BARD]),0,STONESKINS)
	CheckStat(NearestEnemyOfType([GOODCUTOFF.0.0.BARD]),0,SANCTUARY)
	See(NearestEnemyOfType([GOODCUTOFF.0.0.BARD]))
	False()
THEN
  	RESPONSE #100
END
// Nearest DRUID_ALL.
IF
	!CheckStat(NearestEnemyOfType([GOODCUTOFF.0.0.DRUID_ALL]),0,STONESKINS)
	!StateCheck(NearestEnemyOfType([GOODCUTOFF.0.0.DRUID_ALL]),STATE_IMPROVEDINVISIBILITY)
	CheckStat(NearestEnemyOfType([GOODCUTOFF.0.0.DRUID_ALL]),0,SANCTUARY)
	See(NearestEnemyOfType([GOODCUTOFF.0.0.DRUID_ALL]))
	False()
THEN
  	RESPONSE #100
END
// SecondNearest CLERIC_ALL.
IF
	!CheckStat(NearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]),0,STONESKINS)
	!StateCheck(SecondNearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]),STATE_IMPROVEDINVISIBILITY)
	CheckStat(SecondNearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]),0,SANCTUARY)
	See(SecondNearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]))
	False()
THEN
  	RESPONSE #100
END
// Nearest CLERIC_ALL.
IF
	!CheckStat(NearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]),0,STONESKINS)
	!StateCheck(NearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]),STATE_IMPROVEDINVISIBILITY)
	CheckStat(NearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]),0,SANCTUARY)
	See(NearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]))
	False()
THEN
  	RESPONSE #100
END
// SecondNearest MAGE_ALL.
IF
	!CheckStat(NearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]),0,STONESKINS)
	!StateCheck(SecondNearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]),STATE_IMPROVEDINVISIBILITY)
	CheckStat(SecondNearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]),0,SANCTUARY)
	See(SecondNearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]))
	False()
THEN
  	RESPONSE #100
END
// Nearest MAGE_ALL.
IF
	!CheckStat(NearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]),0,STONESKINS)
	!StateCheck(NearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]),STATE_IMPROVEDINVISIBILITY)
	CheckStat(NearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]),0,SANCTUARY)
	See(NearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]))
	False()
THEN
  	RESPONSE #100
END

/////////////////////////////////////////////////////////////////////
// (2) (2) (2) (2)
// HAVE a target with Spell Protections
/////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// (2) Spell: Spell Strike
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_SPELL_STRIKE)
	Allegiance(Myself,ENEMY)
	!Allegiance(LastSeenBy(Myself),ENEMY)
	OR(2)
		HasBounceEffects(LastSeenBy(Myself))
		HasImmunityEffects(LastSeenBy(Myself))
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_SPELL_STRIKE)
END

/////////////////////////////////////////////////////////////////////
// (2) Spell: Pierce Shield
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_PIERCE_SHIELD)
	Allegiance(Myself,ENEMY)
	!Allegiance(LastSeenBy(Myself),ENEMY)
	OR(2)
		HasBounceEffects(LastSeenBy(Myself))
		HasImmunityEffects(LastSeenBy(Myself))
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_PIERCE_SHIELD)
END


/////////////////////////////////////////////////////////////////////
// (2) Spell: Warding Whip 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_WARDING_WHIP)
	Allegiance(Myself,ENEMY)
	!Allegiance(LastSeenBy(Myself),ENEMY)
	OR(2)
		HasBounceEffects(LastSeenBy(Myself))
		HasImmunityEffects(LastSeenBy(Myself))
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_WARDING_WHIP)
END

/////////////////////////////////////////////////////////////////////
// (2) Spell: Ruby Ray
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_RUBY_RAY_OF_REVERSAL)
	Allegiance(Myself,ENEMY)
	!Allegiance(LastSeenBy(Myself),ENEMY)
	OR(2)
		HasBounceEffects(LastSeenBy(Myself))
		HasImmunityEffects(LastSeenBy(Myself))
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_RUBY_RAY_OF_REVERSAL)
END

/////////////////////////////////////////////////////////////////////
// (2) Spell: Secret Word 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_SECRET_WORD)
	Allegiance(Myself,ENEMY)
	!Allegiance(LastSeenBy(Myself),ENEMY)
	OR(2)
		HasBounceEffects(LastSeenBy(Myself))
		HasImmunityEffects(LastSeenBy(Myself))
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_SECRET_WORD)
END

/////////////////////////////////////////////////////////////////////
// (2) Spell: Spell Thrust 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_SPELL_THRUST)
	Allegiance(Myself,ENEMY)
	!Allegiance(LastSeenBy(Myself),ENEMY)
	OR(2)
		HasBounceEffects(LastSeenBy(Myself))
		HasImmunityEffects(LastSeenBy(Myself))
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_SPELL_THRUST)
END

/////////////////////////////////////////////////////////////////////
// Spell: Breach 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_BREACH)
	Allegiance(Myself,ENEMY)
	!Allegiance(LastSeenBy(Myself),ENEMY)
	OR(3)
		HasBounceEffects(LastSeenBy(Myself))
		HasImmunityEffects(LastSeenBy(Myself))
		!CheckStat(LastSeenBy(Myself),0,STONESKINS)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_BREACH)
END

/////////////////////////////////////////////////////////////////////
// (3) (3) (3) (3)
// Find a target with something interesting to dispel
/////////////////////////////////////////////////////////////////////

//  Myself, If Paranoid
IF
	!Global("Paranoid","LOCALS",0)
	!See(NearestEnemyOf(Myself))
	See(Myself)
	False()
THEN
	RESPONSE #100
END

//  SixthNearest.
IF
	OR(8)
	 	!StateCheck(SixthNearestEnemyOf(Myself),STATE_BLESS)
	 	!StateCheck(SixthNearestEnemyOf(Myself),STATE_CHANT)
	 	!StateCheck(SixthNearestEnemyOf(Myself),STATE_DRAWUPONHOLYMIGHT)
	 	!StateCheck(SixthNearestEnemyOf(Myself),STATE_BLUR)
	 	!StateCheck(SixthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
	 	!CheckStat(SixthNearestEnemyOf(Myself),0,STONESKINS)
		HasBounceEffects(SixthNearestEnemyOf(Myself))
		HasImmunityEffects(SixthNearestEnemyOf(Myself))
	!StateCheck(SixthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
	See(SixthNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
//  FifthNearest.
IF
	OR(8)
	 	!StateCheck(FifthNearestEnemyOf(Myself),STATE_BLESS)
	 	!StateCheck(FifthNearestEnemyOf(Myself),STATE_CHANT)
	 	!StateCheck(FifthNearestEnemyOf(Myself),STATE_DRAWUPONHOLYMIGHT)
	 	!StateCheck(FifthNearestEnemyOf(Myself),STATE_BLUR)
	 	!StateCheck(FifthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
	 	!CheckStat(FifthNearestEnemyOf(Myself),0,STONESKINS)
		HasBounceEffects(FifthNearestEnemyOf(Myself))
		HasImmunityEffects(FifthNearestEnemyOf(Myself))
	!StateCheck(FifthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
	See(FifthNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
//  FourthNearest.
IF
	OR(8)
	 	!StateCheck(FourthNearestEnemyOf(Myself),STATE_BLESS)
	 	!StateCheck(FourthNearestEnemyOf(Myself),STATE_CHANT)
	 	!StateCheck(FourthNearestEnemyOf(Myself),STATE_DRAWUPONHOLYMIGHT)
	 	!StateCheck(FourthNearestEnemyOf(Myself),STATE_BLUR)
	 	!StateCheck(FourthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
	 	!CheckStat(FourthNearestEnemyOf(Myself),0,STONESKINS)
		HasBounceEffects(FourthNearestEnemyOf(Myself))
		HasImmunityEffects(FourthNearestEnemyOf(Myself))
	!StateCheck(FourthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
	See(FourthNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
//  ThirdNearest.
IF
	OR(8)
	 	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_BLESS)
	 	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_CHANT)
	 	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_DRAWUPONHOLYMIGHT)
	 	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_BLUR)
	 	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
	 	!CheckStat(ThirdNearestEnemyOf(Myself),0,STONESKINS)
		HasBounceEffects(ThirdNearestEnemyOf(Myself))
		HasImmunityEffects(ThirdNearestEnemyOf(Myself))
	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
	See(ThirdNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
//  SecondNearest.
IF
	OR(8)
	 	!StateCheck(SecondNearestEnemyOf(Myself),STATE_BLESS)
	 	!StateCheck(SecondNearestEnemyOf(Myself),STATE_CHANT)
	 	!StateCheck(SecondNearestEnemyOf(Myself),STATE_DRAWUPONHOLYMIGHT)
	 	!StateCheck(SecondNearestEnemyOf(Myself),STATE_BLUR)
	 	!StateCheck(SecondNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
	 	!CheckStat(SecondNearestEnemyOf(Myself),0,STONESKINS)
		HasBounceEffects(SecondNearestEnemyOf(Myself))
		HasImmunityEffects(SecondNearestEnemyOf(Myself))
	!StateCheck(SecondNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
	See(SecondNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
//  Nearest.
IF
	OR(8)
	 	!StateCheck(NearestEnemyOf(Myself),STATE_BLESS)
	 	!StateCheck(NearestEnemyOf(Myself),STATE_CHANT)
	 	!StateCheck(NearestEnemyOf(Myself),STATE_DRAWUPONHOLYMIGHT)
	 	!StateCheck(NearestEnemyOf(Myself),STATE_BLUR)
	 	!StateCheck(NearestEnemyOf(Myself),STATE_MIRRORIMAGE)
	 	!CheckStat(NearestEnemyOf(Myself),0,STONESKINS)
		HasBounceEffects(NearestEnemyOf(Myself))
		HasImmunityEffects(NearestEnemyOf(Myself))
	!StateCheck(NearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
	See(NearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END

/////////////////////////////////////////////////////////////////////
// (3) (3) (3) (3)
// FOUND a target with something interesting to dispel
/////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// (3) Spell: remove magic
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	Allegiance(Myself,ENEMY)
	OR(2)
		!Global("Paranoid","LOCALS",0)
		!Allegiance(LastSeenBy(Myself),ENEMY)
	HaveSpell(WIZARD_DISPEL_MAGIC)
	OR(8)
		!StateCheck(LastSeenBy(Myself),STATE_BLESS)
		!StateCheck(LastSeenBy(Myself),STATE_CHANT)
		!StateCheck(LastSeenBy(Myself),STATE_DRAWUPONHOLYMIGHT)
		!StateCheck(LastSeenBy(Myself),STATE_BLUR)
		!StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
		!CheckStat(LastSeenBy(Myself),0,STONESKINS)
		HasBounceEffects(LastSeenBy(Myself))
		HasImmunityEffects(LastSeenBy(Myself))
	!RandomNum(20,0)
THEN
	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
	Spell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
END

/////////////////////////////////////////////////////////////////////
// (3) Spell: remove magic (sub fireball)
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	Allegiance(Myself,ENEMY)
	OR(2)
		!Allegiance(LastSeenBy(Myself),ENEMY)
		!Global("Paranoid","LOCALS",0)
	HaveSpell(WIZARD_FIREBALL)
	OR(8)
		!StateCheck(LastSeenBy(Myself),STATE_BLESS)
		!StateCheck(LastSeenBy(Myself),STATE_CHANT)
		!StateCheck(LastSeenBy(Myself),STATE_DRAWUPONHOLYMIGHT)
		!StateCheck(LastSeenBy(Myself),STATE_BLUR)
		!StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
		!CheckStat(LastSeenBy(Myself),0,STONESKINS)
		HasBounceEffects(LastSeenBy(Myself))
		HasImmunityEffects(LastSeenBy(Myself))
	!RandomNum(20,0)
THEN
	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		SetGlobalTimer("Remove","LOCALS",24)
		RemoveSpell(WIZARD_FIREBALL)
		SpellNoDec(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
END

/////////////////////////////////////////////////////////////////////
// (3) Spell: remove magic (sub haste)
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	Allegiance(Myself,ENEMY)
	OR(2)
		!Global("Paranoid","LOCALS",0)
		!Allegiance(LastSeenBy(Myself),ENEMY)
	HaveSpell(WIZARD_HASTE)
	OR(8)
		!StateCheck(LastSeenBy(Myself),STATE_BLESS)
		!StateCheck(LastSeenBy(Myself),STATE_CHANT)
		!StateCheck(LastSeenBy(Myself),STATE_DRAWUPONHOLYMIGHT)
		!StateCheck(LastSeenBy(Myself),STATE_BLUR)
		!StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
		!CheckStat(LastSeenBy(Myself),0,STONESKINS)
		HasBounceEffects(LastSeenBy(Myself))
		HasImmunityEffects(LastSeenBy(Myself))
	!RandomNum(20,0)
THEN
	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		SetGlobalTimer("Remove","LOCALS",24)
		RemoveSpell(WIZARD_HASTE)
		SpellNoDec(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
END

/////////////////////////////////////////////////////////////////////
// (4) (4) (4) (4)
// Find a summoned creature. 
/////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// Spell: Death Spell
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_DEATH_SPELL)
	See(NearestEnemyOfType([GOODCUTOFF.0.0.0.0.SUMMONED]))
	!RandomNum(20,0)
THEN
	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_DEATH_SPELL)
END

/////////////////////////////////////////////////////////////////////
// From here on in we must have enemies
/////////////////////////////////////////////////////////////////////
IF
	OR(2)
	 	!Detect(NearestEnemyOf(Myself))
	 	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		NoAction()
END

/////////////////////////////////////////////////////////////////////
// (5) (5) (5) (5)
// Find a target with no spell protections & low magic resistance. 
/////////////////////////////////////////////////////////////////////

//  SixthNearest.
IF
	!HasBounceEffects(SixthNearestEnemyOf(Myself))
	!HasImmunityEffects(SixthNearestEnemyOf(Myself))
	CheckStatLT(SixthNearestEnemyOf(Myself),50,RESISTMAGIC)
    //!StateCheck(SixthNearestEnemyOf(Myself),STATE_OUT_OF_ACTION)
	!StateCheck(SixthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
	See(SixthNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
//  FifthNearest.
IF
	!HasBounceEffects(FifthNearestEnemyOf(Myself))
	!HasImmunityEffects(FifthNearestEnemyOf(Myself))
	CheckStatLT(FifthNearestEnemyOf(Myself),50,RESISTMAGIC)
    //!StateCheck(FifthNearestEnemyOf(Myself),STATE_OUT_OF_ACTION)
	!StateCheck(FifthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
	See(FifthNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
//  FourthNearest.
IF
	!HasBounceEffects(FourthNearestEnemyOf(Myself))
	!HasImmunityEffects(FourthNearestEnemyOf(Myself))
	CheckStatLT(FourthNearestEnemyOf(Myself),50,RESISTMAGIC)
    //!StateCheck(FourthNearestEnemyOf(Myself),STATE_OUT_OF_ACTION)
	!StateCheck(FourthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
	See(FourthNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
//  ThirdNearest.
IF
	!HasBounceEffects(ThirdNearestEnemyOf(Myself))
	!HasImmunityEffects(ThirdNearestEnemyOf(Myself))
	CheckStatLT(ThirdNearestEnemyOf(Myself),50,RESISTMAGIC)
    //!StateCheck(ThirdNearestEnemyOf(Myself),STATE_OUT_OF_ACTION)
	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
	See(ThirdNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
//  SecondNearest.
IF
	!HasBounceEffects(SecondNearestEnemyOf(Myself))
	!HasImmunityEffects(SecondNearestEnemyOf(Myself))
	CheckStatLT(SecondNearestEnemyOf(Myself),50,RESISTMAGIC)
    //!StateCheck(SecondNearestEnemyOf(Myself),STATE_OUT_OF_ACTION)
	!StateCheck(SecondNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
	See(SecondNearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
//  Nearest.
IF
	!HasBounceEffects(NearestEnemyOf(Myself))
	!HasImmunityEffects(NearestEnemyOf(Myself))
	CheckStatLT(NearestEnemyOf(Myself),50,RESISTMAGIC)
    //!StateCheck(NearestEnemyOf(Myself),STATE_OUT_OF_ACTION)
	!StateCheck(NearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
	CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
	See(NearestEnemyOf(Myself))
	False()
THEN
  	RESPONSE #100
END
//  Nearest CLERIC_ALL.
IF
	!HasBounceEffects(NearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]))
	!HasImmunityEffects(NearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]))
	CheckStatLT(NearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]),50,RESISTMAGIC)
    //!StateCheck(NearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL])),STATE_OUT_OF_ACTION)
	!StateCheck(NearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]),STATE_IMPROVEDINVISIBILITY)
	CheckStat(NearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]),0,SANCTUARY)
	See(NearestEnemyOfType([GOODCUTOFF.0.0.CLERIC_ALL]))
	False()
THEN
  	RESPONSE #100
END
//  Nearest MAGE_ALL.
IF
	!HasBounceEffects(NearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]))
	!HasImmunityEffects(NearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]))
    //!StateCheck(NearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL])),STATE_OUT_OF_ACTION)
	!StateCheck(NearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]),STATE_IMPROVEDINVISIBILITY)
	CheckStat(NearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]),0,SANCTUARY)
	See(NearestEnemyOfType([GOODCUTOFF.0.0.MAGE_ALL]))
	False()
THEN
  	RESPONSE #100
END

/////////////////////////////////////////////////////////////////////
// From here on in we must have enemies
/////////////////////////////////////////////////////////////////////
IF
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		NoAction()
END

IF
	Allegiance(LastSeenBy(Myself),ENEMY)
THEN
	RESPONSE #100
		NoAction()
END


/////////////////////////////////////////////////////////////////////
// Spell: Spell Trigger 
/////////////////////////////////////////////////////////////////////
IF
	CheckStatGT(Myself,15,LEVEL)
	GlobalTimerExpired("Cast","LOCALS")
	Global("Trigger","LOCALS",0)
	CheckStatLT(LastSeenBy(Myself),50,RESISTELECTRICITY)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobal("Trigger","LOCALS",1)
		DisplayString(Myself,26243) // Spell Trigger
		ReallyForceSpell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
		ReallyForceSpell(LastSeenBy(Myself),WIZARD_CHAIN_LIGHTNING)
		ReallyForceSpell(LastSeenBy(Myself),WIZARD_CHAOS)
END

/////////////////////////////////////////////////////////////////////
// Spell: Imprisonment
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_IMPRISONMENT)
	Range(LastSeenBy(Myself),2)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_IMPRISONMENT)
END

/////////////////////////////////////////////////////////////////////
// Spell: Malison
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_GREATER_MALISON)
	CheckStatLT(LastSeenBy(Myself),5,SAVEVSSPELL)
	!RandomNum(20,0)
THEN
	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_GREATER_MALISON)
END

/////////////////////////////////////////////////////////////////////
// Spell: Wilting 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_ABI_DALZIMS_HORRID_WILTING)
	CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

/////////////////////////////////////////////////////////////////////
// Spell: Wilting (sub Simul)
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_SIMULACRUM)
	CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		RemoveSpell(WIZARD_SIMULACRUM)
		SpellNoDec(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

/////////////////////////////////////////////////////////////////////
// Spell: Wilting (sub Summon Fiend)
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_SUMMON_FIEND)
	CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		RemoveSpell(WIZARD_SUMMON_FIEND)
		SpellNoDec(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

/////////////////////////////////////////////////////////////////////
// Spell: Wilting (sub Maze )
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_MAZE)
	CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		RemoveSpell(WIZARD_MAZE)
		SpellNoDec(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

/////////////////////////////////////////////////////////////////////
// Spell: Banshee
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_WAIL_OF_THE_BANSHEE)
	Range(LastSeenBy(Myself),30)
	CheckStatGT(LastSeenBy(Myself),4,SAVEVSSPELL)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(Myself,WIZARD_WAIL_OF_THE_BANSHEE)
END

/////////////////////////////////////////////////////////////////////
// Spell: Symbol Stun
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	OR(2)
		HaveSpell(WIZARD_SYMBOL_STUN)
		HaveSpell(WIZARD_NPC_SYMBOL_STUN)
	CheckStatGT(LastSeenBy(Myself),4,SAVEVSSPELL)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_SYMBOL_STUN)
		Spell(LastSeenBy(Myself),WIZARD_NPC_SYMBOL_STUN)
END

/////////////////////////////////////////////////////////////////////
// Spell: Power Word Stun
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_POWER_WORD_STUN)
	CheckStatGT(LastSeenBy(Myself),4,SAVEVSSPELL)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_POWER_WORD_STUN)
END

/////////////////////////////////////////////////////////////////////
// Spell: Symbol Death
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	OR(2)
		HaveSpell(WIZARD_SYMBOL_DEATH)
		HaveSpell(WIZARD_NPC_SYMBOL_DEATH)
	CheckStatGT(LastSeenBy(Myself),4,SAVEVSSPELL)
	CheckStat(LastSeenBy(Myself),0,SCRIPTINGSTATE2) // Death Ward
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_SYMBOL_DEATH)
		Spell(LastSeenBy(Myself),WIZARD_NPC_SYMBOL_DEATH)
END

/////////////////////////////////////////////////////////////////////
// Spell: PWK
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_POWER_WORD_KILL)
	CheckStatGT(LastSeenBy(Myself),4,SAVEVSSPELL)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_POWER_WORD_KILL)
END

/////////////////////////////////////////////////////////////////////
// Spell: Symbol Fear
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	OR(2)
		HaveSpell(WIZARD_SYMBOL_FEAR)
		HaveSpell(WIZARD_NPC_SYMBOL_FEAR)
	CheckStatGT(LastSeenBy(Myself),4,SAVEVSSPELL)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy,WIZARD_SYMBOL_FEAR)
		Spell(LastSeenBy,WIZARD_NPC_SYMBOL_FEAR)
END

/////////////////////////////////////////////////////////////////////
// Spell: Mordy
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_MORDENKAINENS_SWORD)
	CheckStatGT(LastSeenBy(Myself),-15,ARMORCLASS)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(Myself,WIZARD_MORDENKAINENS_SWORD)
END

/////////////////////////////////////////////////////////////////////
// Spell: Mordy (instead of sphere of chaos)
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_SPHERE_OF_CHAOS)
	CheckStatGT(LastSeenBy(Myself),-15,ARMORCLASS)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		RemoveSpell(WIZARD_SPHERE_OF_CHAOS)
		SpellNoDec(Myself,WIZARD_MORDENKAINENS_SWORD)
END

/////////////////////////////////////////////////////////////////////
// Spell: Mordy (instead of Efreet)
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_SUMMON_EFREET)
	CheckStatGT(LastSeenBy(Myself),-15,ARMORCLASS)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		RemoveSpell(WIZARD_SUMMON_EFREET)
		SpellNoDec(Myself,WIZARD_MORDENKAINENS_SWORD)
END

/////////////////////////////////////////////////////////////////////
// Spell: Mordy (instead of Prism)
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_PRISMATIC_SPRAY)
	CheckStatGT(LastSeenBy(Myself),-15,ARMORCLASS)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		RemoveSpell(WIZARD_PRISMATIC_SPRAY)
		SpellNoDec(Myself,WIZARD_MORDENKAINENS_SWORD)
END

/////////////////////////////////////////////////////////////////////
// Spell: Chaos
/////////////////////////////////////////////////////////////////////
IF		
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_CHAOS)
	CheckStatGT(LastSeenBy(Myself),1,SAVEVSSPELL)
	!StateCheck(LastSeenBy(Myself),STATE_BERSERK) // Minsc Berserk
	CheckStat(LastSeenBy(Myself),0,SCRIPTINGSTATE3) // Barbarian Rage
	!HasItemEquiped("AMUL17",LastSeenBy(Myself)) // greenstone
	!HasItemEquiped("LEAT20",LastSeenBy(Myself)) // aeger's hide
	!HasItemEquiped("SHLD25",LastSeenBy(Myself)) // Shield of Harmony +2
	!HasItemEquipedReal("SW1H54",LastSeenBy(Myself)) // equalizer
	!HasItemEquipedReal("SW2H14",LastSeenBy(Myself)) // lilarcor
	!HasItemEquipedReal("SW2H21",LastSeenBy(Myself)) // psion's blade
    //!StateCheck(LastSeenBy(Myself),STATE_OUT_OF_ACTION)
	!RandomNum(20,0)
THEN
	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_CHAOS)
END

/////////////////////////////////////////////////////////////////////
// Spell: FOD
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_FINGER_OF_DEATH)
	CheckStatGT(LastSeenBy(Myself),1,SAVEVSSPELL)
	CheckStat(LastSeenBy(Myself),0,SCRIPTINGSTATE2) // Death Ward
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_FINGER_OF_DEATH)
END

/////////////////////////////////////////////////////////////////////
// Mental Domination
/////////////////////////////////////////////////////////////////////
IF		
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_DOMINATION)
	CheckStatGT(LastSeenBy(Myself),1,SAVEVSSPELL)
	!StateCheck(LastSeenBy(Myself),STATE_BERSERK) // Minsc Berserk
	CheckStat(LastSeenBy(Myself),0,SCRIPTINGSTATE3) // Barbarian Rage
	!Class(LastSeenBy(Myself),MONK)
	!Kit(LastSeenBy(Myself),INQUISITOR)
	!HasItemEquiped("HELM06",LastSeenBy(Myself)) // Helm of Charm Protection
	!HasItemEquiped("MISCBC",LastSeenBy(Myself)) // Blackrazor
	!HasItemEquiped("NPCHAN",LastSeenBy(Myself)) // Corthala Family Armor
	!HasItemEquiped("REAVER",LastSeenBy(Myself)) // Unholy Reaver
	!HasItemEquiped("SHLD25",LastSeenBy(Myself)) // Shield of Harmony +2
	!HasItemEquipedReal("SPER07",LastSeenBy(Myself)) // Spear of the Unicorn +2		 !HasItemEquipedReal("STAF11",LastSeenBy(Myself)) // Staff of the Magi
	!HasItemEquipedReal("SW1H35",LastSeenBy(Myself)) // Adjatha the Drinker +2
	!HasItemEquipedReal("SW1H54",LastSeenBy(Myself)) // The Equalizer
	!HasItemEquipedReal("SW2H14",LastSeenBy(Myself)) // Lilarcor
    //!StateCheck(LastSeenBy(Myself),STATE_OUT_OF_ACTION)
	!RandomNum(20,0)
THEN
	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_DOMINATION)
END

/////////////////////////////////////////////////////////////////////
// Polymorph Other
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_POLYMORPH_OTHER)
	CheckStatGT(LastSeenBy(Myself),1,SAVEVSPOLY)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_POLYMORPH_OTHER)
END

/////////////////////////////////////////////////////////////////////
// Confusion 
/////////////////////////////////////////////////////////////////////
IF		
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_CONFUSION)
	CheckStatGT(LastSeenBy(Myself),1,SAVEVSSPELL)
	!StateCheck(LastSeenBy(Myself),STATE_BERSERK) // Minsc Berserk
	CheckStat(LastSeenBy(Myself),0,SCRIPTINGSTATE3) // Barbarian Rage
	!HasItemEquiped("AMUL17",LastSeenBy(Myself)) // greenstone
	!HasItemEquiped("LEAT20",LastSeenBy(Myself)) // aeger's hide
	!HasItemEquiped("SHLD25",LastSeenBy(Myself)) // Shield of Harmony +2
	!HasItemEquipedReal("SW1H54",LastSeenBy(Myself)) // equalizer
	!HasItemEquipedReal("SW2H14",LastSeenBy(Myself)) // lilarcor
	!HasItemEquipedReal("SW2H21",LastSeenBy(Myself)) // psion's blade
    // Don't use too much on downed foes.
    //!StateCheck(LastSeenBy(Myself),STATE_OUT_OF_ACTION)
	!RandomNum(20,0)
THEN
	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_CONFUSION)
END

/////////////////////////////////////////////////////////////////////
// Spell: Flesh to Stone
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_FLESH_TO_STONE)
	CheckStatGT(LastSeenBy(Myself),1,SAVEVSPOLY)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_FLESH_TO_STONE)
END

/////////////////////////////////////////////////////////////////////
// Spell: Disintegrate 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_DISINTEGRATE)
	CheckStatGT(LastSeenBy(Myself),1,SAVEVSSPELL)
	CheckStat(LastSeenBy(Myself),0,SCRIPTINGSTATE2) // Death Ward
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_DISINTEGRATE)
END

/////////////////////////////////////////////////////////////////////
// Spell: CHAIN LIGHTNING 
/////////////////////////////////////////////////////////////////////
IF		
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_CHAIN_LIGHTNING)
	CheckStatLT(LastSeenBy(Myself),50,RESISTELECTRICITY)
	!HasItemEquiped("CLCK24",LastSeenBy(Myself)) // Cloak of reflection
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
    //!StateCheck(LastSeenBy(Myself),STATE_OUT_OF_ACTION)
	!RandomNum(20,0)
THEN
	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_CHAIN_LIGHTNING)
END

/////////////////////////////////////////////////////////////////////
// Cone of Cold
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_CONE_OF_COLD)
	CheckStatLT(LastSeenBy(Myself),50,RESISTCOLD)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_CONE_OF_COLD)
END

/////////////////////////////////////////////////////////////////////
// Spell: Hold Person 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_HOLD_PERSON)
	CheckStatGT(LastSeenBy(Myself),1,SAVEVSSPELL)
	!Kit(LastSeenBy(Myself),INQUISITOR)
	!HasItemEquiped("FREERING",LastSeenBy(Myself)) // Ring of Free Action
	!HasItemEquiped("ROSSRING",LastSeenBy(Myself)) // Ring of Free Action
	!HasItemEquiped("RING09",LastSeenBy(Myself)) // Ring of Free Action
	!HasItemEquiped("NPSW01",LastSeenBy(Myself)) // Sword of Avoreen
	OR(6) // only slow fighter-types
		Class(LastSeenBy(Myself),RANGER_ALL)
		Class(LastSeenBy(Myself),FIGHTER_ALL)
		Class(LastSeenBy(Myself),PALADIN)
		Class(LastSeenBy(Myself),THIEF)
		Kit(LastSeenBy(Myself),BARBARIAN)
		Gender(LastSeenBy(Myself),SUMMONED)
    //!StateCheck(LastSeenBy(Myself),STATE_OUT_OF_ACTION)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_HOLD_PERSON)
END

/////////////////////////////////////////////////////////////////////
// Flame Arrow 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_FLAME_ARROW)
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
END

/////////////////////////////////////////////////////////////////////
// Skull Trap 
/////////////////////////////////////////////////////////////////////
IF		
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_SKULL_TRAP)
	CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
    // Don't use too much on downed foes.
    //!StateCheck(LastSeenBy(Myself),STATE_OUT_OF_ACTION)
	!RandomNum(20,0)
THEN
	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_SKULL_TRAP)
END

/////////////////////////////////////////////////////////////////////
// Spell: Sunfire
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_SUN_FIRE)
	Range(LastSeenBy(Myself),30)
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(Myself,WIZARD_SUN_FIRE)
END

/////////////////////////////////////////////////////////////////////
// Bolt 
/////////////////////////////////////////////////////////////////////
IF		
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_LIGHTNING_BOLT)
	CheckStatLT(LastSeenBy(Myself),50,RESISTELECTRICITY)
	!HasItemEquiped("CLCK24",LastSeenBy(Myself)) // Cloak of reflection
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
    // Don't use too much on downed foes.
    //!StateCheck(LastSeenBy(Myself),STATE_OUT_OF_ACTION)
	!RandomNum(20,0)
THEN
	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_LIGHTNING_BOLT)
END

/////////////////////////////////////////////////////////////////////
// Spell: Mislead
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("Shadow","LOCALS")
	HaveSpell(WIZARD_MISLEAD)
	See(Myself)
	OR(3)
		See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
		Global("Paranoid","LOCALS",1)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		SetGlobalTimer("Shadow","LOCALS",32)
		Spell(Myself,WIZARD_MISLEAD)
END

/////////////////////////////////////////////////////////////////////
// Spell: Shadow Door
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("Shadow","LOCALS")
	HaveSpell(WIZARD_SHADOW_DOOR)
	See(Myself)
	OR(3)
		See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
		Global("Paranoid","LOCALS",1)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		SetGlobalTimer("Shadow","LOCALS",32)
		Spell(Myself,WIZARD_SHADOW_DOOR)
END

/////////////////////////////////////////////////////////////////////
// Healing Potions
/////////////////////////////////////////////////////////////////////
IF
	!Detect(NearestEnemyOf(Myself))
	HPPercentLT(Myself,100)
	GlobalTimerExpired("Cast","LOCALS")
THEN
	RESPONSE #100
    GiveItemCreate("potn52",Myself,1,0,0)
    UseItem("potn52",Myself)
    SetGlobalTimer("Cast","LOCALS",6)
END



/////////////////////////////////////////////////////////////////////
// Spell: Animate Dead
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_ANIMATE_DEAD)
	CheckStatGT(LastSeenBy(Myself),-10,ARMORCLASS)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_ANIMATE_DEAD)
END


/////////////////////////////////////////////////////////////////////
// Spell: Invisible Stalker 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_INVISIBLE_STALKER)
	CheckStatGT(LastSeenBy(Myself),-10,ARMORCLASS)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_INVISIBLE_STALKER)
END

/////////////////////////////////////////////////////////////////////
// Spell: Wyvern Call 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_WYVERN_CALL)
	CheckStatGT(LastSeenBy(Myself),-10,ARMORCLASS)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_WYVERN_CALL)
END

/////////////////////////////////////////////////////////////////////
// Spell: Spider Spawn 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_SPIDER_SPAWN)
	CheckStatGT(LastSeenBy(Myself),-10,ARMORCLASS)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_SPIDER_SPAWN)
END

/////////////////////////////////////////////////////////////////////
// Spell: Monster Summoning 3
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_MONSTER_SUMMONING_3)
	CheckStatGT(LastSeenBy(Myself),-10,ARMORCLASS)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_MONSTER_SUMMONING_3)
END

/////////////////////////////////////////////////////////////////////
// Spell: Fire Sheild 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	GlobalTimerExpired("Fire","LOCALS")
	HaveSpell(WIZARD_FIRE_SHIELD_RED)
	See(Myself)
	OR(3)
		See(NearestEnemyOf(Myself))
		HPPercentLT(Myself,100)
		Global("Paranoid","LOCALS",1)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		SetGlobalTimer("Fire","LOCALS",32)
		Spell(Myself,WIZARD_FIRE_SHIELD_RED)
END


/////////////////////////////////////////////////////////////////////
// Slow 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_SLOW)
	CheckStatGT(LastSeenBy(Myself),1,SAVEVSSPELL)
	!Class(LastSeenBy(Myself),MONK)
	!HasItemEquiped("FREERING",LastSeenBy(Myself)) // Ring of Free Action
	!HasItemEquiped("ROSSRING",LastSeenBy(Myself)) // Ring of Free Action
	!HasItemEquiped("RING09",LastSeenBy(Myself)) // Ring of Free Action
	!HasItemEquiped("NPSW01",LastSeenBy(Myself)) // Sword of Avoreen
	OR(6) // only slow fighter-types
		Class(LastSeenBy(Myself),RANGER_ALL)
		Class(LastSeenBy(Myself),FIGHTER_ALL)
		Class(LastSeenBy(Myself),PALADIN)
		Class(LastSeenBy(Myself),THIEF)
		Kit(LastSeenBy(Myself),BARBARIAN)
		Gender(LastSeenBy(Myself),SUMMONED)
    //!StateCheck(LastSeenBy(Myself),STATE_OUT_OF_ACTION)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_SLOW)
END

/////////////////////////////////////////////////////////////////////
// Web
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_WEB)
	CheckStatGT(LastSeenBy(Myself),1,SAVEVSSPELL)
	!Kit(LastSeenBy(Myself),INQUISITOR)
	!HasItemEquiped("ANTIWEB",LastSeenBy(Myself)) // is a spider
	!HasItemEquiped("FREERING",LastSeenBy(Myself)) // Ring of Free Action
	!HasItemEquiped("ROSSRING",LastSeenBy(Myself)) // Ring of Free Action
	!HasItemEquiped("RING09",LastSeenBy(Myself)) // Ring of Free Action
	!HasItemEquiped("NPSW01",LastSeenBy(Myself)) // Sword of Avoreen
    //////!StateCheck(LastSeenBy(Myself),STATE_OUT_OF_ACTION)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_WEB)
END

/////////////////////////////////////////////////////////////////////
// Stinking Cloud
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_STINKING_CLOUD)
	CheckStatGT(LastSeenBy(Myself),1,SAVEVSSPELL)
	!Kit(LastSeenBy(Myself),INQUISITOR)
	!HasItemEquiped("ANTIWEB",LastSeenBy(Myself)) // is a spider
	!HasItemEquiped("FREERING",LastSeenBy(Myself)) // Ring of Free Action
	!HasItemEquiped("ROSSRING",LastSeenBy(Myself)) // Ring of Free Action
	!HasItemEquiped("RING09",LastSeenBy(Myself)) // Ring of Free Action
	!HasItemEquiped("NPSW01",LastSeenBy(Myself)) // Sword of Avoreen
    //!StateCheck(LastSeenBy(Myself),STATE_OUT_OF_ACTION)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_STINKING_CLOUD)
END

/////////////////////////////////////////////////////////////////////
// Spell: Cloudkill
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_CLOUDKILL)
	HasItem("LICH",Myself)  // immune to spell level 5!
	CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_CLOUDKILL)
END

/////////////////////////////////////////////////////////////////////
// Melf Acid Arrow 
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_MELF_ACID_ARROW)
	CheckStatLT(LastSeenBy(Myself),50,RESISTACID)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
END

/////////////////////////////////////////////////////////////////////
// Schorcher
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_AGANNAZAR_SCORCHER)
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_AGANNAZAR_SCORCHER)
END

/////////////////////////////////////////////////////////////////////
// Orb 
/////////////////////////////////////////////////////////////////////
IF		
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_CHROMATIC_ORB)
	CheckStatGT(LastSeenBy(Myself),6,SAVEVSSPELL)
	CheckStatLT(LastSeenBy(Myself),50,RESISTACID)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
    // Don't use too much on downed foes.
	!RandomNum(20,0)
THEN
	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_CHROMATIC_ORB)
END

/////////////////////////////////////////////////////////////////////
// Spell: Monster Summoning 2
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_MONSTER_SUMMONING_2)
	CheckStatGT(LastSeenBy(Myself),-5,ARMORCLASS)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_MONSTER_SUMMONING_2)
END

/////////////////////////////////////////////////////////////////////
// Spell: Monster Summoning 1
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
	HaveSpell(WIZARD_MONSTER_SUMMONING_1)
	CheckStatGT(LastSeenBy(Myself),-5,ARMORCLASS)
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_MONSTER_SUMMONING_1)
END

/////////////////////////////////////////////////////////////////////
// Magic Missile
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_MAGIC_MISSILE)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
END

/////////////////////////////////////////////////////////////////////
// Minor Drain
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(WIZARD_LARLOCH_MINOR_DRAIN)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!RandomNum(20,0)
THEN
  	RESPONSE #100
		SetGlobalTimer("Cast","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_LARLOCH_MINOR_DRAIN)
END


/////////////////////////////////////////////////////////////////////
// Use those meteors ... the half-attack or "cast and attack" method
/////////////////////////////////////////////////////////////////////
IF
	HasItem("melfmet", Myself)
	!HasBounceEffects(LastSeenBy(Myself))
	!GlobalTimerExpired("Cast","LOCALS")
	ActionListEmpty()
	!StateCheck(Myself,STATE_INVISIBLE)
THEN
	RESPONSE #100
		EquipRanged()
		Attack(LastSeenBy(Myself))
END

// Don't let any other script do something stupid!
IF
	ActionListEmpty()
THEN
	RESPONSE #100
		RandomWalk()
END
